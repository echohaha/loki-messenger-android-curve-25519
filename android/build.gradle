buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

archivesBaseName = "curve25519-android"
version          = version_number
group            = group_info

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${archivesBaseName}-${version}.aar"
                output.outputFileName = fileName
            }
        }
    }
}

repositories {
    google()
    jcenter()
}

dependencies {
    implementation project(':java')
    androidTestImplementation (project(":tests")) {
        exclude module: 'junit'
    }
}

signing {
    required { hasProperty("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "") {
            authentication(userName: "", password: "")
        }

        pom.project {
            name 'curve25519-android'
            packaging 'aar'
            description 'Curve25519 library for Android'
            url 'https://github.com/loki-project/loki-messenger-android-curve-25519'

            scm {
                url 'scm:git@github.com:loki-project/loki-messenger-android-curve-25519.git'
                connection 'scm:git@github.com:loki-project/loki-messenger-android-curve-25519.git'
                developerConnection 'scm:git@github.com:loki-project/loki-messenger-android-curve-25519.git'
            }

            licenses {
                license {
                    name 'GPLv3'
                    url 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    name 'Niels Andriesse'
                }
            }
        }
    }
}

task installArchives(type: Upload) {
    description "Installs the artifacts to the local Maven repository."
    configuration = configurations['archives']
    repositories {
        mavenDeployer {
            repository url: "file://${System.properties['user.home']}/.m2/repository"
        }
    }
}

